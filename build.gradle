plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.9-SNAPSHOT' // cant go beyond this until Java 17
	id 'io.spring.dependency-management' version '1.1.0'
    id("org.sonarqube") version "3.5.0.2730" // cant go beyond this until Java 11
	id 'jacoco'
	id "org.owasp.dependencycheck" version "8.3.1"
}

group = 'com.ldiamond'
version = '4.0.0-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'org.springframework:spring-web'
	implementation group: 'commons-codec', name: 'commons-codec'

	implementation group: 'org.knowm.xchart', name: 'xchart', version: '3.8.5'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.13.0'
	implementation group: 'com.github.librepdf', name: 'openpdf', version: '1.3.30'
	implementation("com.google.guava:guava:32.1.2-jre")

	testImplementation 'com.tngtech.archunit:archunit-junit5:1.1.0'
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.11.0' // cant go beyond this until Java 11
}

tasks.named('test') {
	useJUnitPlatform()
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.login", "squ_35a3bb236089d69724749ee5735d279e1223a921"
        property "sonar.scm.disabled", "true"
		property "sonar.sources", "src/main/java/com/ldiamond/sqgraph"
		property "sonar.coverage.exclusions", 
			"src/main/java/com/ldiamond/sqgraph/Config.java,src/main/java/com/ldiamond/sqgraph/SearchHistory.java,src/main/java/com/ldiamond/sqgraph/ValidationResult.java,src/main/java/com/ldiamond/sqgraph/MetricsResults.java"
    }
}


jacoco {
    toolVersion = "0.8.8"
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

// Note, on 1 January 2024 this project will abandon support for Java 8, as SonarQube itself requires 17 on the server and 11 on the client and Spring now requires Java 17.
// Java 8 market share is below 33% as this is written and will likely be below 25% by 2024.   

